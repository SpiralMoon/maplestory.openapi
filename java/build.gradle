plugins {
    id 'java-library'
    id 'signing'
    id 'maven-publish'
    id 'org.jreleaser' version '1.17.0'
}

group = 'dev.spiralmoon'
version = '3.6.0'

repositories {
    mavenCentral()
}

dependencies {
    compileOnly 'org.projectlombok:lombok:1.18.38'

    implementation 'org.jsoup:jsoup:1.20.1'
    implementation 'com.squareup.retrofit2:retrofit:3.0.0'
    implementation 'com.squareup.retrofit2:converter-jackson:3.0.0'
    implementation 'com.squareup.retrofit2:converter-scalars:3.0.0'
    implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.20.0'

    testImplementation platform('org.junit:junit-bom:5.10.1')
    testImplementation 'org.junit.jupiter:junit-jupiter-api'
    testImplementation 'org.assertj:assertj-core:3.24.2'

    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine'

    annotationProcessor 'org.projectlombok:lombok:1.18.38'
}

task unitTest(type: Test) {
    def apiKeyKms = project.findProperty('API_KEY_KMS')
    def apiKeyMsea = project.findProperty('API_KEY_MSEA')
    def apiKeyTms = project.findProperty('API_KEY_TMS')
    def accessToken = project.findProperty('ACCESS_TOKEN')
    systemProperty('API_KEY_KMS', apiKeyKms)
    systemProperty('API_KEY_MSEA', apiKeyMsea)
    systemProperty('API_KEY_TMS', apiKeyTms)
    systemProperty('ACCESS_TOKEN', accessToken)

    useJUnitPlatform()
}

task sourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    from sourceSets.main.allSource
}

task javadocJar(type: Jar) {
    archiveClassifier.set('javadoc')
    from tasks.javadoc.destinationDir
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar

            groupId = 'dev.spiralmoon'
            version = '3.6.0'
            artifactId = 'maplestory-openapi'

            pom {
                name.set('MapleStory OpenAPI')
                description.set('This Java library enables the use of the MapleStory OpenAPI of Nexon.')
                url.set('https://github.com/SpiralMoon/maplestory.openapi')

                licenses {
                    license {
                        name.set('MIT License')
                        url.set('https://opensource.org/licenses/MIT')
                    }
                }

                developers {
                    developer {
                        id.set('SpiralMoon')
                        name.set('SpiralMoon')
                        email.set('dev.spiralmoon@gmail.com')
                    }
                }

                scm {
                    connection.set('scm:git:git://github.com/SpiralMoon/maplestory.openapi.git')
                    developerConnection.set('scm:git:ssh://github.com:SpiralMoon/maplestory.openapi.git')
                    url.set('https://github.com/SpiralMoon/maplestory.openapi')
                }
            }
        }
    }

    repositories {
        maven {
            url = layout.buildDirectory.dir('staging-deploy')
        }
    }
}

def githubToken = project.findProperty('githubToken')

def gpgPublicKey = project.findProperty('gpgPublicKey')
def gpgSecretKey = project.findProperty('gpgSecretKey')
def gpgPassphrase = project.findProperty('gpgPassphrase')

def centralUsername = project.findProperty('centralUsername')
def centralPassword = project.findProperty('centralPassword')

jreleaser {
    project {
        gitRootSearch = true
    }

    release {
        github {
            token.set(githubToken)
        }
    }

    signing {
        active = 'ALWAYS'
        armored = true
        mode = 'MEMORY'
        publicKey.set(gpgPublicKey)
        secretKey.set(gpgSecretKey)
        passphrase.set(gpgPassphrase)
    }

    deploy {
        maven {
            mavenCentral {
                sonatype {
                    active = 'ALWAYS'
                    url = 'https://central.sonatype.com/api/v1/publisher'
                    username.set(centralUsername)
                    password.set(centralPassword)
                    stagingRepository('build/staging-deploy')
                }
            }
        }
    }
}
